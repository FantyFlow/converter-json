plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.serialization'
    id 'maven-publish'
    id 'signing'
}
android {
    namespace 'com.retrofit2.converter'
    compileSdk 33
    buildToolsVersion = '33.0.0'

    defaultConfig {
        minSdk 23
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = 'io.github.fantyflow'
            artifactId = 'converter-json'
            version = '1.5.0'

            pom {
                name = 'JsonConverterFactory'
                description = 'A Retrofit 2 Converter.Factory for Kotlin Serialization.'
                url = 'https://github.com/FantyFlow/converter-json'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'fan1138612367'
                        name = 'FantyFlow'
                        email = 'fan1138612367@vip.qq.com'
                    }
                }

                scm {
                    connection = 'https://github.com/FantyFlow/converter-json.git'
                    developerConnection = 'https://github.com/FantyFlow/converter-json.git'
                    url = 'https://github.com/FantyFlow/converter-json'
                }
            }

            afterEvaluate {
                from components.release
            }
        }
    }
    repositories {
        maven {
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username ossrhUsername
                password ossrhPassword
            }
        }
    }
}
signing {
    sign publishing.publications.release
}

dependencies {

    implementation 'com.squareup.okhttp3:okhttp:4.10.0'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.5.0'
}